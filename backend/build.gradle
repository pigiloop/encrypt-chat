plugins {
    id 'java'
    id("io.freefair.lombok") version "9.0.0"
    id 'jacoco'
    id 'checkstyle'
    id("io.qameta.allure") version "3.0.1"
    id 'maven-publish'
}

group = 'ru.vinhome'
version = "${backend_module_version}"

ext {
    nexusUrl = System.getenv('NEXUS_URL') ?: project.findProperty('nexus_url')
    nexusUsername = System.getenv('NEXUS_USERNAME') ?: project.findProperty('nexus_username')
    nexusPassword = System.getenv('NEXUS_PASSWORD') ?: project.findProperty('nexus_password')
}

repositories {
    maven {
        allowInsecureProtocol = true // будем ходить по http в nexus
        url = "${nexusUrl}/repository/maven-central/" // куда мы идем?

        credentials {
            username nexusUsername
            password nexusPassword
        }
    }

    // maven snapshot
    maven {
        allowInsecureProtocol = true // будем ходить по http в nexus
        url = "${nexusUrl}/repository/maven-snapshots/" // куда мы идем?

        credentials {
            username nexusUsername
            password nexusPassword
        }
    }

    // maven release
    maven {
        allowInsecureProtocol = true // будем ходить по http в nexus
        url = "${nexusUrl}/repository/maven-releases/" // куда мы идем?

        credentials {
            username nexusUsername
            password nexusPassword
        }
    }
}

dependencies {
    // server
    implementation "org.eclipse.jetty:jetty-server:${jetty_version}"
    implementation "org.eclipse.jetty.ee10:jetty-ee10-servlet:${jetty_version}"

    implementation "org.glassfish.jersey.core:jersey-server:${jersey_server_version}"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:${jersey_server_version}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jersey_server_version}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jersey_server_version}"

    implementation "org.glassfish.jaxb:jaxb-runtime:${jaxb_runtime_version}"

    implementation "ch.qos.logback:logback-core:${logback_version}"
    implementation "ch.qos.logback:logback-classic:${logback_version}"

    implementation "org.postgresql:postgresql:${jdbc_postgres_driver_version}"

// https://mvnrepository.com/artifact/org.testcontainers/postgresql
    testImplementation "org.testcontainers:postgresql:${testcontainers_postgresql_version}"

    testImplementation "io.qameta.allure:allure-junit5:${allure_junit5_version}"

    testImplementation platform("org.junit:junit-bom:${junit_bom_version}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }
}

jacocoTestCoverageVerification {
    dependsOn build
    violationRules {
        {
            rule {
                {
                    limit {
                        minimum = 0.80
                    }
                }
            }
        }
    }
}

checkstyle {
    toolVersion = "10.12.4"
    configFile = file("config/checkstyle/checkstyle.xml")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            group group
            artifactId "${backend_module_name}"
            version version
        }
    }

    repositories {
        maven {
            allowInsecureProtocol = true
            url = version.endsWith("SNAPSHOT")
                    ? "${nexusUrl}/repository/maven-snapshots/"
                    : "${nexusUrl}/repository/maven-releases/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification, javadoc
}